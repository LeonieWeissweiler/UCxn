strat main { 
  Seq(
    Onf(conditionals),
    Onf(interrogatives),
    Onf(resultatives)
    )
}

package conditionals {
  rule r4 { 
    pattern {
      IF [lemma="如果"|"假如"|"假設"|"假使"|"倘若"|"倘使"];
      PROTASIS -[mark]-> IF;
      _anchor_ -[advcl]->PROTASIS;
    }
    commands {
      _anchor_.Cxn=Conditional-Generic;
      _anchor_.CxnElt="Apodosis";
      PROTASIS.CxnElt="Protasis";
    }
  }
}

package interrogatives {

  rule r2a { % yes/no questions with 吗 (ma) particle
    pattern { 
      _anchor_->QP;
      QP[form="嗎"];
    }
    commands { 
      _anchor_.Cxn=Interrogative-Polar-Direct;
      _anchor_.CxnElt=Clause
    }
  }


  rule r2b { % yes/no questions with positive/negative/positive structure, the anchor is the first verb of the repetition
    pattern { 
      NEG[form="不"|"沒"]; 
        _anchor_->REPETITION; 
        _anchor_-[1=conj]->REPETITION; 
        REPETITION-[advmod]->NEG;     
        REPETITION->NEG; 
        _anchor_.form = REPETITION.form; 
        REPETITION>>_anchor_; 
        REPETITION>>NEG
    }
    without {
        REPETITION->INTER; INTER<<REPETITION; INTER>>_anchor_;
        % This is for removing such cases :  我是不小心，而不是故意的。
        % but keeping such cases : 中国菜好吃不好吃 
        }
    commands { 
      _anchor_.Cxn=Interrogative-Polar-Direct;
      _anchor_.CxnElt=Clause
    }
  }


  rule r2c { % Content questions with questions words (什么 ， 谁 …) (upos = PRON for excluding wh-indefinite) when the WH-word IS NOT the head of the sentence
    pattern { 
      e: _anchor_-> Q;
      Q[lemma="怎"|"怎麼"|"什麼"|"誰"|"為什麼"|"哪兒"|"哪裡"|"哪裏"|"幾"|"多少"|"哪"|"幹麼"|"怎樣"|"甚麼"];
    }
    with {
        e.label <> "root";
        P[lemma="？"];
    }
    commands { 
      _anchor_.Cxn="Interrogative-WHInfo-Direct";
      _anchor_.CxnElt="Clause";
      Q.CxnElt="WHWord";
    }
  }

    rule r2cbis { % Content questions with questions words (什么 ， 谁 …) (upos = PRON for excluding wh-indefinite) when the WH-word IS the head of the sentence
      pattern { 
        e: ROOT-> _anchor_;
        _anchor_[lemma="怎"|"怎麼"|"什麼"|"誰"|"為什麼"|"哪兒"|"哪裡"|"哪裏"|"幾"|"多少"|"哪"|"幹麼"|"怎樣"|"甚麼"];
      }
      with {
          e.label = "root";
          P[lemma="？"];
      }
      commands { 
        _anchor_.Cxn="Interrogative-WHInfo-Direct";
          _anchor_.CxnElt="Clause";
          _anchor_.CxnElt="WHWord";
      }
  }

  rule r2d { % Matching based on the question mark only AND removing all other strategies
    pattern {
      _anchor_->PUNCT;
      PUNCT[lemma="？"];
    }
    without {
      MA[form="嗎"]
    }
    without { 
      WH [lemma="怎"|"怎麼"|"什麼"|"誰"|"為什麼"|"哪兒"|"哪裡"|"哪裏"|"幾"|"多少"|"哪"|"幹麼"|"怎樣"|"甚麼"]; 
    }
    without { 
      _anchor_ [lemma="怎"|"怎麼"|"什麼"|"誰"|"為什麼"|"哪兒"|"哪裡"|"哪裏"|"幾"|"多少"|"哪"|"幹麼"|"怎樣"|"甚麼"]; 
    }
    without { 
      M[form="還是"];
    }
    commands {
      _anchor_.Cxn=Interrogative;
      _anchor_.CxnElt=Clause
    }
  }
}

package resultatives {

  rule r1 {
    pattern { 
      _anchor_ -[compound:vv]-> RES;
    }
    commands { 
      _anchor_.Cxn=Resultative;
      _anchor_.CxnElt=Event;
      RES.CxnElt=ResultState;
    }
  }

}

%package comparatives {
%  rule r3 {
%    pattern {
%        Bi [lemma="比"];
%    }
%    commands { Bi.Cxn=Comparative }
%  }
%}

%package existentials {
  % Existential
  % we still get around 50% false positive, so we comment it at the moment
  %rule r5 { 
  %  pattern { 
  %    YOU [form="有"]; YOU-[obj]-> OBJ ;
  %  }
  %  without {
  %    YOU-[nsubj]->SUBJ;
  %  } 
  %  commands {YOU.Cxn=Existential}
  %}
%}